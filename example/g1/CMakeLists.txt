add_executable(g1_loco_client high_level/g1_loco_client_example.cpp)
target_link_libraries(g1_loco_client unitree_sdk2)

add_executable(g1_arm5_sdk_dds_example high_level/g1_arm5_sdk_dds_example.cpp)
target_link_libraries(g1_arm5_sdk_dds_example unitree_sdk2)

add_executable(g1_arm7_sdk_dds_example high_level/g1_arm7_sdk_dds_example.cpp)
target_link_libraries(g1_arm7_sdk_dds_example unitree_sdk2)

add_executable(g1_ankle_swing_example low_level/g1_ankle_swing_example.cpp)
target_link_libraries(g1_ankle_swing_example unitree_sdk2)

add_executable(g1_audio_client_example audio/g1_audio_client_example.cpp)
target_link_libraries(g1_audio_client_example unitree_sdk2)

find_package(Eigen3 3.3 QUIET NO_MODULE)
if(Eigen3_FOUND)
    message(STATUS "Eigen3 found - building g1_dex3_example")
    add_executable(g1_dex3_example dex3/g1_dex3_example.cpp)
    target_link_libraries(g1_dex3_example unitree_sdk2 Eigen3::Eigen)
else()
    message(WARNING "Eigen3 not found - skipping g1_dex3_example. Install with: apt-get install libeigen3-dev")
endif()

find_package(Boost COMPONENTS program_options)
if(Boost_FOUND)
    message(STATUS "Boost found - building g1_termination and g1_arm_action_example")
    add_executable(g1_termination low_level/terminations.cpp)
    target_link_libraries(g1_termination unitree_sdk2 Boost::program_options)

    add_executable(g1_arm_action_example high_level/g1_arm_action_example.cpp)
    target_link_libraries(g1_arm_action_example unitree_sdk2 Boost::program_options)
else()
    message(WARNING "Boost not found - skipping g1_termination and g1_arm_action_example. Install with: apt-get install libboost-all-dev")
endif()

find_package(yaml-cpp QUIET)
if(yaml-cpp_FOUND)
    if (${yaml-cpp_VERSION} VERSION_GREATER_EQUAL "0.6")
        message(STATUS "Found yaml-cpp version ${yaml-cpp_VERSION} - building g1_dual_arm_example")
        add_executable(g1_dual_arm_example low_level/g1_dual_arm_example.cpp)
        target_link_libraries(g1_dual_arm_example PRIVATE unitree_sdk2 yaml-cpp)
        target_compile_definitions(g1_dual_arm_example PUBLIC BLIB_DIR="${CMAKE_CURRENT_SOURCE_DIR}/low_level/behavior_lib/")
    else()
        message(WARNING "yaml-cpp version ${yaml-cpp_VERSION} is too old (requires >= 0.6), skipping g1_dual_arm_example")
    endif()
else()
    message(WARNING "yaml-cpp not found - skipping g1_dual_arm_example. Install with: apt-get install libyaml-cpp-dev")
endif()

add_executable(g1_agv_client_example agv/g1_agv_client_example.cpp)
target_link_libraries(g1_agv_client_example unitree_sdk2)
